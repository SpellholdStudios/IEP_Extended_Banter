// --------------------------------------
// Interaction Expansion Project:
// Extended NPC-NPC Interaction
//
// Version:   v5.3
// --------------------------------------
// Backup directory
BACKUP ~iepbanters/backup~

// Error reports sent to K'aeloree
AUTHOR ~K'aeloree (kae@spellholdstudios.net)~

VERSION ~V5.4~

README ~iepbanters/iepbanters_readme.html~

AUTO_TRA ~iepbanters/%s~

ALWAYS
  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SET bg2_chapter = 12
  END ELSE BEGIN
    OUTER_SET bg2_chapter = 0
  END
  OUTER_FOR (i=1; i<=10; i=i+1) BEGIN
    OUTER_SET bg2_chapter = bg2_chapter + 1
    OUTER_SPRINT name_source ~bg2_chapter_%i%~
    OUTER_SET EVAL ~%name_source%~ = bg2_chapter
  END

  // No exception, everything needs to be converted
  ACTION_DEFINE_ARRAY IEPnoconvert BEGIN END

  // any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
  ACTION_DEFINE_ARRAY IEPreload BEGIN Setup END

  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charsets = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%/Translations~
      noconvert_array = IEPnoconvert
      reload_array = IEPreload
  END
END



LANGUAGE ~English~
         ~English~
         ~iepbanters/translations/english/setup.tra~
LANGUAGE ~Deutsch~
         ~German~
         ~iepbanters/translations/german/setup.tra~
LANGUAGE ~Russian~
         ~Russian~
         ~iepbanters/translations/russian/setup.tra~
LANGUAGE ~Francais~
         ~french~
         ~iepbanters/translations/french/setup.tra~
LANGUAGE ~Chinese~
         ~schinese~
         ~iepbanters/translations/schinese/setup.tra~

// --------------------------------------
// IEP Extended NPC-NPC Interaction
// --------------------------------------
BEGIN @1

ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
// Add custom If Valid For Party Dialogue state
APPEND ~state.ids~
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~
  
// Add Imoen's banter file if Fixpack isn't installed
ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ THEN BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME ~saradush.mve~ THEN BEGIN
      COPY_EXISTING ~interdia.2da~ ~override~
        SET_2DA_ENTRY 17 1 2 ~BIMOEN2~
        BUT_ONLY_IF_IT_CHANGES
    END ELSE BEGIN
      APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~
        UNLESS ~BIMOEN2~
    END
  END
  
// Gives Imoen a banter file if there isn't one already
ACTION_IF NOT FILE_EXISTS_IN_GAME ~bimoen2.dlg~ THEN BEGIN
  <<<<<<<< iepbanters/dialogue/bimoen2.d
  BEGIN BIMOEN2
  >>>>>>>>
  COMPILE ~iepbanters/dialogue/bimoen2.d~
END

END

// Ensures banters won't run in BG1 section in Baldur's Gate Trilogy
ACTION_IF FILE_EXISTS ~DATA/BG1ARE.BIF~ THEN BEGIN
    OUTER_SPRINT "percentage_sign" "%"
    OUTER_SPRINT ~BGT_VAR~ ~Global("ENDOFBG1","GLOBAL",2)~
END ELSE BEGIN
    OUTER_SPRINT "percentage_sign" "%"
    OUTER_SPRINT ~BGT_VAR~ ~~
END

// Compile dialogue
COMPILE EVALUATE_BUFFER ~iepbanters/dialogue/banters.d~
USING ~iepbanters/translations/english/banters.tra~
	~iepbanters/translations/%s/banters.tra~

// Extend and compile scripts
EXTEND_BOTTOM ~ar0606.bcs~  ~iepbanters/scripts/ar0606.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~ar0804.bcs~  ~iepbanters/scripts/ar0804.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~aerie.bcs~   ~iepbanters/scripts/aerie.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~jaheira.bcs~ ~iepbanters/scripts/jaheira.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~mazzy.bcs~   ~iepbanters/scripts/mazzy.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~minsc.bcs~   ~iepbanters/scripts/minsc.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~vicond.bcs~   ~iepbanters/scripts/vicond.baf~
  EVALUATE_BUFFER
EXTEND_BOTTOM ~viconia.bcs~  ~iepbanters/scripts/viconia.baf~
  EVALUATE_BUFFER
  
ACTION_IF FILE_EXISTS_IN_GAME     ~FWJASSY.DLG~ THEN BEGIN
  EXTEND_BOTTOM ~minsc.bcs~   ~iepbanters/scripts/minsc_2.baf~
    EVALUATE_BUFFER
END

ACTION_IF NOT FILE_EXISTS_IN_GAME ~FWJASSY.DLG~ THEN BEGIN
  EXTEND_BOTTOM ~minsc.bcs~   ~iepbanters/scripts/minsc_3.baf~
    EVALUATE_BUFFER
END
  
// Copying over blank audio file
ACTION_IF NOT FILE_EXISTS ~override/i#blank.wav~ THEN BEGIN
  COPY ~iepbanters/audio/i#blank.wav~ ~override~
END


// --------------------------------------
// Extended Minsc/Aerie Interaction ToB
// --------------------------------------
BEGIN @2
  REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @14

// Add custom If Valid For Party Dialogue state
APPEND ~state.ids~
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

// Compile dialogue
COMPILE ~iepbanters/dialogue/minscaerie_tob.d~
USING ~iepbanters/translations/english/minscaerie_tob.tra~
	~iepbanters/translations/%s/minscaerie_tob.tra~


                  
                  
// --------------------------------------
// Imoen "Retrospection" dialogue for BGII: ToB
// --------------------------------------
BEGIN @3
  REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @15

APPEND ~STATE.IDS~ // adds custom IsValidForPartyDialogue state
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

// Compiling dialogue...
COMPILE ~iepbanters/dialogue/imoen25j.d~
USING ~iepbanters/translations/english/imoen25j.tra~
	~iepbanters/translations/%s/imoen25j.tra~


// Appending scripts...
EXTEND_BOTTOM ~imoe25.bcs~ ~iepbanters/scripts/imoe25.baf~



// --------------------------------------
// Anomen Romance "Wake Up" Dialogue
// --------------------------------------
BEGIN @4

// Compiling dialogue...
COMPILE ~iepbanters/dialogue/banomen.d~
USING ~iepbanters/translations/english/banomen.tra~
	~iepbanters/translations/%s/banomen.tra~

// Appending scripts...
EXTEND_BOTTOM ~anomen.bcs~ ~iepbanters/scripts/anomen_romance.baf~

ACTION_IF MOD_IS_INSTALLED ~SETUP-NPCFLIRT.TP2~ 12 // Anomen Flirt Pack for SOA
THEN BEGIN
  COPY_EXISTING ~anomenj.dlg~ ~override~
    DECOMPILE_DLG_TO_D
      REPLACE_TEXTUALLY ~Global("FWAnomenMorningAfter","LOCALS",1)~
                        ~Global("FWAnomenMorningAfter","LOCALS",9999)~
    COMPILE_D_TO_DLG
  BUT_ONLY_IF_IT_CHANGES
END



// --------------------------------------
// Extended NPC Conflicts
// --------------------------------------
BEGIN @5

// Add custom If Valid For Party Dialogue state
APPEND ~state.ids~
  ~0x80101FEF CD_STATE_NOTVALID~
  UNLESS ~CD_STATE_NOTVALID~

// Compile dialogue
COMPILE ~iepbanters/dialogue/conflicts.d~
USING ~iepbanters/translations/english/conflicts.tra~
	~iepbanters/translations/%s/conflicts.tra~



// --------------------------------------
// Restored NPC Interjections
// --------------------------------------
BEGIN @13

// DADROW21 interjection
COPY_EXISTING ~DADROW21.DLG~ ~override~
  DECOMPILE_DLG_TO_D
    REPLACE_TEXTUALLY ~!Global("AerieRomanceActive","GLOBAL",1)~
                      ~~
    REPLACE_TEXTUALLY ~!Global("AerieRomanceActive","GLOBAL",2)~
                      ~~
    REPLACE_TEXTUALLY ~!Global("AnomenRomanceActive","GLOBAL",1)~
                      ~~
    REPLACE_TEXTUALLY ~!Global("AnomenRomanceActive","GLOBAL",2)~
                      ~~
    REPLACE_TEXTUALLY ~!Global("JaheiraRomanceActive","GLOBAL",1)~
                      ~~
    REPLACE_TEXTUALLY ~!Global("JaheiraRomanceActive","GLOBAL",2)~
                      ~~
  COMPILE_D_TO_DLG
BUT_ONLY_IF_IT_CHANGES

COMPILE ~iepbanters/dialogue/restored_interjections.d~


// --------------------------------------
// Banter Accelerator
//
// Originally author: jcompton
// Modified with permission by: K'aeloree
// --------------------------------------

// 10 minutes
BEGIN @6
  SUBCOMPONENT @7
    // Uninstalling all previous Banter Accelerators if installed
    UNINSTALL Setup-banterpack.tp2 1
    UNINSTALL Setup-banterpack.tp2 3
    UNINSTALL Setup-bg1npc.tp2 25
    UNINSTALL Kelsey.tp2 3
    UNINSTALL Keto.tp2 2
    // Appending gtimes.ids to include the timer
    APPEND ~gtimes.ids~ ~600 I#BANTER_TIMER~
    EXTEND_TOP ~baldur.bcs~ ~iepbanters/scripts/baldur.baf~ EVALUATE_BUFFER
    
    ACTION_IF FILE_EXISTS ~data/25dialog.bif~ THEN BEGIN
      EXTEND_TOP ~baldur25.bcs~ ~iepbanters/scripts/baldur.baf~
    END

// 15 minutes
BEGIN @8
  SUBCOMPONENT @7
    // Uninstalling all previous Banter Accelerators if installed
    UNINSTALL Setup-banterpack.tp2 1
    UNINSTALL Setup-banterpack.tp2 3
    UNINSTALL Setup-bg1npc.tp2 25
    UNINSTALL Kelsey.tp2 3
    UNINSTALL Keto.tp2 2
    // Appending gtimes.ids to include the timer
    APPEND ~gtimes.ids~ ~900 I#BANTER_TIMER~
    EXTEND_TOP ~baldur.bcs~ ~iepbanters/scripts/baldur.baf~ EVALUATE_BUFFER

    ACTION_IF FILE_EXISTS ~data/25dialog.bif~ THEN BEGIN
      EXTEND_TOP ~baldur25.bcs~ ~iepbanters/scripts/baldur.baf~
    END

// 20 minutes
BEGIN @9
  SUBCOMPONENT @7
    // Uninstalling all previous Banter Accelerators if installed
    UNINSTALL Setup-banterpack.tp2 1
    UNINSTALL Setup-banterpack.tp2 3
    UNINSTALL Setup-bg1npc.tp2 25
    UNINSTALL Kelsey.tp2 3
    UNINSTALL Keto.tp2 2
    // Appending gtimes.ids 
    APPEND ~gtimes.ids~ ~1200 I#BANTER_TIMER~
    EXTEND_TOP ~baldur.bcs~ ~iepbanters/scripts/baldur.baf~ EVALUATE_BUFFER

    ACTION_IF FILE_EXISTS ~data/25dialog.bif~ THEN BEGIN
      EXTEND_TOP ~baldur25.bcs~ ~iepbanters/scripts/baldur.baf~
    END

// 30 minutes
BEGIN @10
  SUBCOMPONENT @7
    // Uninstalling all previous Banter Accelerators if installed
    UNINSTALL Setup-banterpack.tp2 1
    UNINSTALL Setup-banterpack.tp2 3
    UNINSTALL Setup-bg1npc.tp2 25
    UNINSTALL Kelsey.tp2 3
    UNINSTALL Keto.tp2 2
    // Appending gtimes.ids to include the timer
    APPEND ~gtimes.ids~ ~1800 I#BANTER_TIMER~
    EXTEND_TOP ~baldur.bcs~ ~iepbanters/scripts/baldur.baf~ EVALUATE_BUFFER

    ACTION_IF FILE_EXISTS ~data/25dialog.bif~ THEN BEGIN
      EXTEND_TOP ~baldur25.bcs~ ~iepbanters/scripts/baldur.baf~
    END

// 45 minutes
BEGIN @11
  SUBCOMPONENT @7
    // Uninstalling all previous Banter Accelerators if installed
    UNINSTALL Setup-banterpack.tp2 1
    UNINSTALL Setup-banterpack.tp2 3
    UNINSTALL Setup-bg1npc.tp2 25
    UNINSTALL Kelsey.tp2 3
    UNINSTALL Keto.tp2 2
    // Appending gtimes.ids to include the timer
    APPEND ~gtimes.ids~ ~2700 I#BANTER_TIMER~
    EXTEND_TOP ~baldur.bcs~ ~iepbanters/scripts/baldur.baf~ EVALUATE_BUFFER
    
    ACTION_IF FILE_EXISTS ~data/25dialog.bif~ THEN BEGIN
      EXTEND_TOP ~baldur25.bcs~ ~iepbanters/scripts/baldur.baf~
    END

// 60 minutes
BEGIN @12
  SUBCOMPONENT @7
    // Uninstalling all previous Banter Accelerators if installed
    UNINSTALL Setup-banterpack.tp2 1
    UNINSTALL Setup-banterpack.tp2 3
    UNINSTALL Setup-bg1npc.tp2 25
    UNINSTALL Kelsey.tp2 3
    UNINSTALL Keto.tp2 2
    // Appending gtimes.ids to include the timer
    APPEND ~gtimes.ids~ ~3600 I#BANTER_TIMER~
    EXTEND_TOP ~baldur.bcs~ ~iepbanters/scripts/baldur.baf~ EVALUATE_BUFFER
    
    ACTION_IF FILE_EXISTS ~data/25dialog.bif~ THEN BEGIN
      EXTEND_TOP ~baldur25.bcs~ ~iepbanters/scripts/baldur.baf~
    END
