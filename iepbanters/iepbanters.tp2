// --------------------------------------
// Interaction Expansion Project:
// Extended NPC-NPC Interaction
//
// Version:   v5.3
// --------------------------------------
// Backup directory
BACKUP ~iepbanters/backup~

// Error reports sent to K'aeloree
//AUTHOR ~K'aeloree (kae@spellholdstudios.net)~
SUPPORT ~http://www.shsforums.net/forum/530-iep-extended-banters/~

VERSION ~v5.5~

README ~iepbanters/readme/iepbanters-readme-%LANGUAGE%.html~ ~iepbanters/readme/iepbanters-readme-readme-english.html~


ALWAYS

	INCLUDE ~iepbanters/lib/always.tpa~

END


AUTO_TRA ~iepbanters/translations/%s~

LANGUAGE ~English~
         ~english~
         ~iepbanters/translations/english/setup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Toran)~
         ~german~
         ~iepbanters/translations/german/setup.tra~

LANGUAGE ~Russian (translation by Cutter, Hawkmoon, Austin and arcanecoast.ru)~
         ~russian~
         ~iepbanters/translations/russian/setup.tra~

LANGUAGE ~Francais (traduction : Isaya)~
         ~french~
         ~iepbanters/translations/french/setup.tra~

LANGUAGE ~Chinese (translation by Merzbau)~
         ~schinese~
         ~iepbanters/translations/schinese/setup.tra~


/* ================================== *
 *  IEP Extended NPC-NPC Interaction  *
 * ================================== */
BEGIN @1
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.
DESIGNATED 0 LABEL ~iep_banters~

// Add Imoen's banter file if Fixpack isn't installed
// --------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~cdbehbla.pro~ BEGIN
	ACTION_IF (is_tob OR is_ee) BEGIN 
		COPY_EXISTING ~interdia.2da~ ~override~
			SET_2DA_ENTRY 17 1 2 ~BIMOEN2~ // fixes ToB version
		BUT_ONLY
	END ELSE BEGIN
		APPEND ~interdia.2da~ ~IMOEN       BIMOEN2~ UNLESS ~[ %TAB%]BIMOEN2[ %TAB%%LNL%%MNL%%WNL%]~ // fixes SoA version
	END
END

// Gives Imoen a banter file if there isn't one already
// ----------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~bimoen2.dlg~ BEGIN
	<<<<<<<< iepbanters/dialogue/bimoen2.d
  BEGIN BIMOEN2
  >>>>>>>>
	COMPILE ~iepbanters/dialogue/bimoen2.d~
END

// Compile dialogues
// -----------------
COMPILE EVALUATE_BUFFER ~iepbanters/dialogue/banters.d~
USING ~iepbanters/translations/english/banters.tra~
      ~iepbanters/translations/%s/banters.tra~

// Extend and compile scripts
// --------------------------
EXTEND_BOTTOM ~ar0606.bcs~  ~iepbanters/scripts/ar0606.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~ar0804.bcs~  ~iepbanters/scripts/ar0804.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~aerie.bcs~   ~iepbanters/scripts/aerie.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~jaheira.bcs~ ~iepbanters/scripts/jaheira.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~mazzy.bcs~   ~iepbanters/scripts/mazzy.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~minsc.bcs~   ~iepbanters/scripts/minsc.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~vicond.bcs~  ~iepbanters/scripts/vicond.baf~ EVALUATE_BUFFER
EXTEND_BOTTOM ~viconia.bcs~ ~iepbanters/scripts/viconia.baf~ EVALUATE_BUFFER
  
ACTION_IF FILE_EXISTS_IN_GAME ~FWJASSY.DLG~ BEGIN
	EXTEND_BOTTOM ~minsc.bcs~ ~iepbanters/scripts/minsc_2.baf~ EVALUATE_BUFFER
END ELSE BEGIN
	EXTEND_BOTTOM ~minsc.bcs~ ~iepbanters/scripts/minsc_3.baf~ EVALUATE_BUFFER
END

// Copying over blank audio file
// -----------------------------
ACTION_IF NOT FILE_EXISTS ~override/i#blank.wav~ BEGIN
	COPY ~iepbanters/audio/i#blank.wav~ ~override~
END



/* ====================================== *
 *  Extended Minsc/Aerie Interaction ToB  *
 * ====================================== */
BEGIN @2
REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @14 // ToB is not installed! Please install the Throne of Bhaal expansion before installing the extended ToB Minsc/Aerie interaction.
DESIGNATED 1 LABEL ~iep_banters_minsc_aerie_tob~

// Compile dialogue
COMPILE ~iepbanters/dialogue/minscaerie_tob.d~
USING ~iepbanters/translations/english/minscaerie_tob.tra~
      ~iepbanters/translations/%s/minscaerie_tob.tra~



/* ============================================== *
 *  Imoen "Retrospection" dialogue for BGII: ToB  *
 * ============================================== */
BEGIN @3
REQUIRE_PREDICATE GAME_IS ~bg2ee bgt tob eet~ @15 // ToB is not installed! Please install the Throne of Bhaal expansion before installing this component
DESIGNATED 2 LABEL ~iep_banters_imoen_restropection_tob~

// Compiling dialogue
COMPILE ~iepbanters/dialogue/imoen25j.d~
USING ~iepbanters/translations/english/imoen25j.tra~
      ~iepbanters/translations/%s/imoen25j.tra~

// Appending scripts
EXTEND_BOTTOM ~imoe25.bcs~ ~iepbanters/scripts/imoe25.baf~



/* =================================== *
 *  Anomen Romance "Wake Up" Dialogue  *
 * =================================== */
BEGIN @4
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.
DESIGNATED 3 LABEL ~iep_banters_anomen_romance_wakeup~

// Compiling dialogue
COMPILE ~iepbanters/dialogue/banomen.d~
USING ~iepbanters/translations/english/banomen.tra~
      ~iepbanters/translations/%s/banomen.tra~

// Appending scripts
EXTEND_BOTTOM ~anomen.bcs~ ~iepbanters/scripts/anomen_romance.baf~

// Anomen Flirt Pack for SOA
ACTION_IF MOD_IS_INSTALLED ~SETUP-NPCFLIRT.TP2~ 12 BEGIN
	COPY_EXISTING ~anomenj.dlg~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY ~Global("FWAnomenMorningAfter","LOCALS",1)~ ~Global("FWAnomenMorningAfter","LOCALS",9999)~
		END
	BUT_ONLY
END



/* ======================== *
 *  Extended NPC Conflicts  *
 * ======================== */
BEGIN @5
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.
DESIGNATED 4 LABEL ~iep_npc_conflicts_revision~

// Compiling dialogue
COMPILE ~iepbanters/dialogue/conflicts.d~
USING ~iepbanters/translations/english/conflicts.tra~
      ~iepbanters/translations/%s/conflicts.tra~



/* ========================================= *
 *  Restored NPC Interjections in Underdark  *
 * ========================================= */
BEGIN @13
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.
DESIGNATED 5 LABEL ~iep_restored_npc_interjection_underdark~

// DADROW21 interjection
COPY_EXISTING ~dadrow21.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~!Global("AerieRomanceActive","GLOBAL",1)~ ~~
		REPLACE_TEXTUALLY ~!Global("AerieRomanceActive","GLOBAL",2)~ ~~
		REPLACE_TEXTUALLY ~!Global("AnomenRomanceActive","GLOBAL",1)~ ~~
		REPLACE_TEXTUALLY ~!Global("AnomenRomanceActive","GLOBAL",2)~ ~~
		REPLACE_TEXTUALLY ~!Global("JaheiraRomanceActive","GLOBAL",1)~ ~~
		REPLACE_TEXTUALLY ~!Global("JaheiraRomanceActive","GLOBAL",2)~ ~~
	END
BUT_ONLY

COMPILE ~iepbanters/dialogue/restored_interjections.d~



/* ======================================== *
 *  Banter Accelerator                      *
 *                                          *
 *  Originally author: jcompton             *
 *  Modified with permission by: K'aeloree  *
 * ======================================== */

// 10 minutes
// ----------
BEGIN @7
DESIGNATED 6 LABEL ~iep_banter_accelerator_10~
SUBCOMPONENT @6
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.

// Appending gtimes.ids to include the timer
APPEND ~gtimes.ids~ ~600 I#BANTER_TIMER~

INCLUDE ~iepbanters/lib/iepbanters_accelerator.tpa~


// 15 minutes
// ----------
BEGIN @8
DESIGNATED 7 LABEL ~iep_banter_accelerator_15~
SUBCOMPONENT @6
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.

APPEND ~gtimes.ids~ ~900 I#BANTER_TIMER~

INCLUDE ~iepbanters/lib/iepbanters_accelerator.tpa~


// 20 minutes
// ----------
BEGIN @9
DESIGNATED 8 LABEL ~iep_banter_accelerator_20~
SUBCOMPONENT @6
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.

APPEND ~gtimes.ids~ ~1200 I#BANTER_TIMER~

INCLUDE ~iepbanters/lib/iepbanters_accelerator.tpa~


// 30 minutes
// ----------
BEGIN @10
DESIGNATED 9 LABEL ~iep_banter_accelerator_30~
SUBCOMPONENT @6
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.

APPEND ~gtimes.ids~ ~1800 I#BANTER_TIMER~

INCLUDE ~iepbanters/lib/iepbanters_accelerator.tpa~


// 45 minutes
// ----------
BEGIN @11
DESIGNATED 10 LABEL ~iep_banter_accelerator_45~
SUBCOMPONENT @6
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.

APPEND ~gtimes.ids~ ~2700 I#BANTER_TIMER~

INCLUDE ~iepbanters/lib/iepbanters_accelerator.tpa~


// 60 minutes
// ----------
BEGIN @12
DESIGNATED 11 LABEL ~iep_banter_accelerator_60~
SUBCOMPONENT @6
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @16	// This mod is not available for your game.

APPEND ~gtimes.ids~ ~3600 I#BANTER_TIMER~

INCLUDE ~iepbanters/lib/iepbanters_accelerator.tpa~
